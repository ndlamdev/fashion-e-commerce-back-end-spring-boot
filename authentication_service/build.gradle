plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.5'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

ext {
    springCloudVersion = "2024.0.1"
    springGrpcVersion = "0.7.0"
}

group = 'com.lamnguyen'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'redis.clients:jedis:5.2.0'
    implementation 'com.auth0:java-jwt:4.5.0'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.redisson:redisson-spring-boot-starter:3.45.1'
    implementation 'com.google.api-client:google-api-client:2.7.2'
    implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.google.protobuf:protobuf-java:4.30.2'
}

configurations.configureEach {
    exclude group: 'commons-logging', module: 'commons-logging'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:$springGrpcVersion"
        mavenBom "io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.15.0"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.30.2" // Hoặc bản mới hơn
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.72.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

graalvmNative {
    toolchainDetection = true
    binaries {
        named("main") {
            buildArgs.addAll(
                    "--enable-url-protocols=http",
                    "--report-unsupported-elements-at-runtime",
                    "--no-fallback",
                    "--initialize-at-run-time=sun.net.dns.ResolverConfigurationImpl",
                    "--initialize-at-build-time=io.netty.util.internal.shaded.org.jctools.util.UnsafeAccess",
            )
        }
    }
}

tasks.register('runWithAgent', JavaExec) {
    group = 'native'
    description = 'Run the app with GraalVM agent to collect config'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.lamnguyen.authentication_service.AuthenticationServiceApplication' // ✅ Đổi thành class main thực tế của bạn

    jvmArgs = [
            "-agentlib:native-image-agent=config-output-dir=src/main/resources/META-INF/native-image"
    ]
}
